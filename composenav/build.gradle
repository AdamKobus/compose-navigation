import com.adamkobus.compose.navigation.Lib

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'org.jetbrains.dokka'
}

android {

}

dependencies {
    implementation Lib.AndroidX.Core
    implementation Lib.AndroidX.Appcompat
    implementation Lib.Kotlin.Coroutines
    implementation Lib.Google.Material
    implementation Lib.AdamKobus.LifecycleAwareViewModel

    // compose
    implementation Lib.Compose.Ui
    implementation Lib.Compose.Activity
    implementation Lib.Compose.Material
    implementation Lib.Compose.ToolingPreview
    debugImplementation Lib.Compose.UiTooling

    implementation Lib.Accompanist.NavigationAnimation

    // testing
    testImplementation Lib.Test.JUnit
    testImplementation Lib.Test.Mockk
    testImplementation Lib.Test.CoroutinesTest
}

ext {
    PUBLISH_GROUP_ID = "com.adamkobus"
    PUBLISH_ARTIFACT_ID = "compose-navigation"
}

apply from: "${rootProject.projectDir}/gradle/publishing-module.gradle"

dokkaGfm.configure {
    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)
            reportUndocumented.set(true)
        }
    }
}

tasks.register("removeDocumentation", Delete.class) {
    setGroup("documentation")
    delete "${project.buildDir}/dokka"
}

tasks.register("createDocumentation") {
    setGroup("documentation")
    dependsOn("removeDocumentation", "dokkaGfm")
    mustRunAfter("removeDocumentation")
}

tasks.register("publishDocumentation", Exec.class) {
    setGroup("documentation")
    dependsOn("createDocumentation")
    workingDir "${rootProject.projectDir}"
    commandLine 'python', 'scripts/publish_documentation.py', '-c'
}
