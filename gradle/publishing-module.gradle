apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = rootProject.PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version rootProject.PUBLISH_VERSION

                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar

                pom {
                    name = "Android Compose Navigation"
                    description = 'Utils for integrating Jetpack Navigation with Jetpack Compose'
                    url = 'https://github.com/AdamKobus/compose-navigation'
                    licenses {
                        license {
                            name = 'Android Compose Navigation License'
                            url = 'https://github.com/AdamKobus/compose-navigation/blob/master/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'AdamKobus'
                            name = 'Adam Kobus'
                            email = 'adam.kobus@appson.org'
                        }
                    }

                    scm {
                        connection = 'scm:git:github.com/AdamKobus/compose-navigation.git'
                        developerConnection = 'scm:git:ssh://github.com/AdamKobus/compose-navigation.git'
                        url = 'https://github.com/AdamKobus/compose-navigation/tree/master'
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications
}
